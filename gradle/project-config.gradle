ext.runnableProjects = [
        project(':authorization'),
        project(':configuration'),
        project(':gateway'),
        project(':service-discovery'),
        project(':project-a:api'),
        project(':project-a:ui'),
        project(':project-elastic-search:api')
]

ext.rootProjects = [
        project(':project-a'),
        project(':project-elastic-search')
]

configure(runnableProjects) {
    apply from: "${rootDir}/gradle/archives-upload.gradle"

    springBoot {
        executable = true
    }

    bootRepackage {
        withJarTask = jar
    }

    developers.each { String developer ->
        String taskName = "bootRunDev${developer.capitalize()}"

        task(taskName).doLast {
            bootRun {
                systemProperty 'spring.profiles.active', "dev,$developer"
            }
        }

        tasks.getByName(taskName) {
            finalizedBy bootRun
            group = 'application'
        }
    }
}

configure(subprojects - runnableProjects) {
    tasks.remove(bootRun)

    springBoot {
        executable = false
    }

    bootRepackage {
        enabled = false
    }
}

configure(subprojects - rootProjects) {
    afterEvaluate {
        task buildAll {
            dependsOn build
        }
    }
}